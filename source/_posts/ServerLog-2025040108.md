---
feature: false
title: 服务器日志-2025040108 | 部署内网穿透
date: 2025-04-01 08:00:00
abstracts: 部署 VNT, 一个用 Rust 编写的内网穿透, 打洞成功后通过 P2P 连接
tags:
    - Server
categories:
    - Log
cover: https://fontlos.com/icons/logo.png
---

首先下载服务端程序 [vnt-dev/vnts](https://github.com/vnt-dev/vnts), 启动不需要太多配置

```sh
vnts --web-port <PORT> --username <USER> --password <PASSWD> --port <PORT>
```

其中第一个端口是前端仪表盘, 第二个端口是实际用于穿透监听的

然后在本地机器上安装客户端 [vnt-dev/vnt](https://github.com/vnt-dev/vnt), 其中有两个可执行文件, `vnt-cli` 是完整的, 但是需要管理员权限, 另一个不需要, 但是功能弱一点. 所以使用第一个就好. 对于 Windows 需要在可执行文件目录下载一个 `wintun.dll` 用于作为虚拟网卡

```sh
sudo vnt-cli -s <IP>:<PORT> -k <TOKEN> -n <NAME>
```

其中 IP 和端口来自服务端配置, 使用相同的 Token 接入就会进入同一局域网, Name 用于区分不同的设备

为了在我长期运行的主机上配置自启, 我只想到了这一种很麻烦的方案

首先需要一个 `powershell` 脚本来无窗口启动程序

```sh
# start-hidden.ps1

$scriptPath = Split-Path -Parent $MyInvocation.MyCommand.Definition
$exePath = Join-Path $scriptPath "vnt-cli.exe"
$arguments = "-s <IP>:<PORT> -k <TOKEN> -n <NAME>"

Start-Process -FilePath $exePath -ArgumentList $arguments -WindowStyle Hidden
```

然后因为系统默认不能执行 PS 脚本, 我们使用一个 BAT 拉起它

```bat
:: launch.bat
@echo off
setlocal

set "script_path=%~dp0"

powershell -ExecutionPolicy Bypass -File "%script_path%start-hidden.ps1"
```

然后使用一个 BAT 将其加入到计划任务

```bat
:: install.bat

@echo off
setlocal

:: 检查管理员权限
net session >nul 2>&1
if %errorlevel% neq 0 (
    echo Please run this script as Administrator
    pause
    exit /b
)

:: 设置任务名称和路径
set "task_name=VNT_Background_Launcher"
for %%I in ("%~dp0.") do set "script_path=%%~fI"
set "bat_path=%script_path%\launcher.bat"

:: 删除现有任务
schtasks /delete /tn "%task_name%" /f >nul 2>&1

:: 创建新任务
echo Creating scheduled task...
schtasks /create /tn "%task_name%" /tr "%bat_path%" /sc onstart /ru SYSTEM /rl highest /f

if %errorlevel% neq 0 (
    echo Task creation failed, error: %errorlevel%
    pause
    exit /b 1
)

echo Scheduled task created successfully!
echo Task Name: %task_name%
echo Will run on system startup
pause
```

运行这个 BAT 即可实现开机自启

要取消自启可以手动删除这个计划任务, 也可以再用一个 BAT 脚本

```bat
:: uninstall.bat

@echo off
setlocal

:: 设置任务名称
set "task_name=VNT_Background_Launcher"

:: 删除任务
echo Removing scheduled task...
schtasks /delete /tn "%task_name%" /f

if %errorlevel% equ 0 (
    echo Task removed successfully
) else (
    echo Task removal failed, error: %errorlevel%
)

pause
```
